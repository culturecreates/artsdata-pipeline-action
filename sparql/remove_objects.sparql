PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX schema: <http://schema.org/>
PREFIX schemaHttps: <https://schema.org/>
PREFIX xhtml: <http://www.w3.org/1999/xhtml/vocab#>

DELETE {
  ?s ?p ?o .
  ?s2 ?p2 ?s .
}
WHERE {
  # Step 1: identify resources to delete
  {
    SELECT DISTINCT ?s WHERE {
      {
        ?s rdf:type ?type .
        VALUES ?type {
          schema:SiteNavigationElement
          schemaHttps:SiteNavigationElement
          schema:WPHeader
          schema:WPFooter
          schema:BreadcrumbList
          schema:ListItem
          schema:SearchAction
          schema:EntryPoint
          schema:PropertyValueSpecification
          schema:CollectionPage
        }
      }
      UNION
      {
        ?s xhtml:role ?role .
      }
    }
  }

  # Step 2a: triples where the resource is subject
  OPTIONAL {
    ?s ?p ?o .
  }

  # Step 2b: triples where the resource is object
  OPTIONAL {
    ?s2 ?p2 ?s .
  }
}
